apply plugin: 'groovy'
apply plugin: 'application'

repositories {
    mavenCentral()
    maven { url('http://repo.jenkins-ci.org/releases/') }
}
sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
        }
    }
}

task buildXml {
}

task copyResources(type: Copy) {
    from 'resources'
    include '*.sh'
    into "$project.buildDir/resources"
}

task copyJobs(type: Copy) {
    from 'jobs'
    include '*.gdsl'
    into "$project.buildDir/jobs"
}

fileTree(dir: "${project.projectDir}/jobs", include: '**/*.gdsl').each { file ->

    def baseName = file.name[0..file.name.lastIndexOf('.') - 1]
    def output = new File(project.buildDir, baseName)


    def buildSingleXml = tasks.create(name: "build${baseName.capitalize()}Xml", type: JavaExec) {
        main = 'javaposse.jobdsl.Run'
        classpath = sourceSets.main.runtimeClasspath
        args "${project.buildDir}/jobs/${file.name}"
        workingDir output
    }

    buildSingleXml.doFirst {
        output.mkdirs()
    }

    buildSingleXml.dependsOn copyResources
    buildSingleXml.dependsOn copyJobs

    buildSingleXml.inputs.file file
    buildSingleXml.outputs.dir output

    buildXml.dependsOn buildSingleXml

}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.4.5"
    compile "org.jenkins-ci.plugins:job-dsl:${project.ext.jobDslVersion}@jar"
    compile "org.jenkins-ci.plugins:job-dsl-core:${project.ext.jobDslVersion}"
    compile "org.jenkins-ci.plugins:gradle:${project.ext.gradlePluginVersion}@jar"
    compile 'org.jenkins-ci.plugins:git:2.4.0@jar'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.4'
}
